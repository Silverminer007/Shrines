buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
        classpath group: 'gradle.plugin.com.modrinth.minotaur', name: 'Minotaur', version: '1.2.1'
    }
}

apply plugin: 'com.matthewprenger.cursegradle'

curseforge {
    project {
        apiKey = System.getenv('CURSEFORGE')
        id = '418915'
        //changelog = file('changelog-3.x.x.md')
        def file = file('changelog-3.x.x.md')
        def changelogFile = "## " + file.text
        changelog = changelogFile.substring(0, changelogFile.contains('===') ? changelogFile.indexOf("===") : changelogFile.length())
        changelogType = 'text'
        releaseType = 'beta'
        addGameVersion '1.18'
        addGameVersion '1.18.1'
        addGameVersion 'Forge'
    }
    options {
        detectNewerJava = true // defaults to false
    }
}

import com.modrinth.minotaur.TaskModrinthUpload

task publishModrinth(type: TaskModrinthUpload) { // Make sure it runs after build!
    onlyIf {
        System.getenv("MODRINTH") // Only attempt to run this task if the MODRINTH variable is set, otherwise SKIP it
    }

    versionType = 'beta'
    token = System.getenv("MODRINTH") // An environment property called MODRINTH that is your token, set via Gradle CLI, GitHub Actions, Idea Run Configuration, or other
    projectId = 'F33t24Wi'
    versionNumber = "$project.version"
    versionName = "$project.archivesBaseName-$project.version"
    uploadFile = jar
    def file = file('changelog-3.x.x.md')
    def changelogFile = "## " + file.text
    changelog = changelogFile.substring(0, changelogFile.contains('===') ? changelogFile.indexOf("===") : changelogFile.length())
}