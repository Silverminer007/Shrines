buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url = 'https://maven.parchmentmc.org' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1+', changing: true
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
        // MixinGradle:
        classpath 'org.spongepowered:mixingradle:0.7.+'
        classpath 'org.parchmentmc:librarian:1.+'
        classpath "gradle.plugin.org.cadixdev.gradle:licenser:0.6.+"
    }
}
plugins {
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "2.+"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'org.cadixdev.licenser'

version = "${mc_version}-${mod_version}"
group = 'com.silverminer.shrines'

java {
    archivesBaseName = 'Shrines'
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

license {
    header = project.file('HEADER.txt')
    properties {
        name = 'Silverminer007'
        year = 2022
    }
}

minecraft {
    mappings channel: 'parchment', version: "${parchment_version}-${mc_version}"
    //mappings channel: 'official', version: "${mc_version}"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                shrines {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                shrines {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'shrines', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                shrines {
                    source sourceSets.main
                }
            }
        }
    }
}

curseforge {
    project {
        apiKey = System.getenv('CURSEFORGE')
        id = '418915'
        //changelog = file('changelog-3.x.x.md')
        def file = file('changelog-3.x.x.md')
        def changelogFile = file.text
        changelog = changelogFile.substring(0, changelogFile.contains('===') ? changelogFile.indexOf("===") : changelogFile.length())
        changelogType = 'text'
        if (System.getenv("SHRINES_RELEASE_TYPE") != null) {
            releaseType = System.getenv("SHRINES_RELEASE_TYPE")
        } else {
            releaseType = 'beta'
        }
        addGameVersion '1.18'
        addGameVersion '1.18.1'
        addGameVersion 'Forge'
    }
    options {
        detectNewerJava = true // defaults to false
    }
}

import com.modrinth.minotaur.dependencies.ModDependency
modrinth {
    if (System.getenv("SHRINES_RELEASE_TYPE") != null) {
        versionType = System.getenv("SHRINES_RELEASE_TYPE")
    } else {
        versionType = 'beta'
    }
    token = System.getenv("MODRINTH")
    // An environment property called MODRINTH that is your token, set via Gradle CLI, GitHub Actions, Idea Run Configuration, or other
    projectId = 'F33t24Wi'
    versionNumber = "$project.version"
    versionName = "$project.archivesBaseName-$project.version"
    uploadFile = jar
    def file = file('changelog-3.x.x.md')
    def changelogFile = file.text
    changelog = changelogFile.substring(0, changelogFile.contains('===') ? changelogFile.indexOf("===") : changelogFile.length())
    dependencies = [
            new ModDependency('oz6iM7ld', 'required') // required dep to dynamicregistries
    ]
}

repositories {
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    implementation fg.deobf("maven.modrinth:dynamicregistries:1.18.1-1.0.0")
}

//sourceSets.main.resources { srcDir 'src/generated/resources' }

mixin {
    // Refmaps for each SourceSet
    add sourceSets.main, 'mixins.shrines.refmap.json'

    // Configs to add to runs and jars
    config 'mixins.shrines.json'

    // Specify options for dev run configs
    debug.verbose = true
    debug.export = true
    dumpTargetOnFailure = true

    // Options for the Annotation Processor
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
                "Specification-Title"     : "shrines",
                "Specification-Vendor"    : "Silverm7ner",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "Silverm7ner",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
jar.finalizedBy('reobfJar')
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}