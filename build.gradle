buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}

plugins {
    id "com.modrinth.minotaur" version "2.+"
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.+'
    id "org.cadixdev.licenser" version "0.6.+"
}

apply plugin: 'org.spongepowered.mixin'

version = "${mc_version}-stories-overhaul-preview-4"
//version = "${mc_version}-${System.getenv('shrines_mod_version')}"
group = 'com.silverminer.shrines'

java {
    archivesBaseName = 'Shrines'
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

license {
    header = project.file('HEADER.txt')
    properties {
        name = 'Silverminer007'
        year = 2022
    }
}

minecraft {
    //mappings channel: 'parchment', version: "${parchment_version}-${mc_version}"
    mappings channel: 'official', version: "${mc_version}"

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                shrines {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            args 'nogui'
            mods {
                shrines {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            forceExit false
            args '--existing', file('src/main/resources/')

            mods {
                shrines {
                    source sourceSets.main
                }
            }
        }

        generateData {
            workingDirectory project.file('run')
            parent data
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'shrines', '--all', '--output', file('src/generated/resources/')
            forceExit false

            mods {
                shrines {
                    source sourceSets.main
                }
            }
        }

        worldGenData {
            workingDirectory project.file('run')
            parent data
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'minecraft, shrines', '--reports', '--output', file('src/generated/reports/'), '--flat'
            forceExit false

            mods {
                shrines {
                    source sourceSets.main
                }
            }
        }

        // This run config launches GameTestServer and runs all registered gametests, then exits.
        // By default, the server will crash when no gametests are provided.
        // The gametest system is also enabled by default for other run configs under the /test command.
        gameTestServer {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            property 'forge.enabledGameTestNamespaces', 'shrines'

            property 'forge.enableGameTest', 'true'

            forceExit false

            mods {
                shrines {
                    source sourceSets.main
                }
            }
        }
    }
}

modrinth {
    if (System.getenv("SHRINES_RELEASE_TYPE") != null) {
        versionType = System.getenv("SHRINES_RELEASE_TYPE")
    } else {
        versionType = 'beta'
    }
    token = System.getenv("MODRINTH")
    // An environment property called MODRINTH that is your token, set via Gradle CLI, GitHub Actions, Idea Run Configuration, or other
    projectId = 'F33t24Wi'
    versionNumber = "$project.version"
    versionName = "$project.archivesBaseName-$project.version"
    uploadFile = jar
    def file = file('changelog.md')
    def changelogFile = file.text
    changelog = changelogFile.substring(0, changelogFile.contains('===') ? changelogFile.indexOf("===") : changelogFile.length())
    gameVersions = ["1.19"]
}

curseforge {
    project {
        apiKey = System.getenv('CURSEFORGE')
        id = '418915'
        //changelog = file('changelog.md')
        def file = file('changelog.md')
        def changelogFile = file.text
        changelog = changelogFile.substring(0, changelogFile.contains('===') ? changelogFile.indexOf("===") : changelogFile.length())
        changelogType = 'markdown'
        if (System.getenv("SHRINES_RELEASE_TYPE") != null) {
            releaseType = System.getenv("SHRINES_RELEASE_TYPE")
        } else {
            releaseType = 'beta'
        }
        addGameVersion '1.19'
        addGameVersion 'Forge'
    }
    options {
        detectNewerJava = true // defaults to false
    }
}

repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    //runtimeOnly fg.deobf("curse.maven:huge-structure-blocks-474114:3656200")
}

mixin {
    // MixinGradle Settings
    add sourceSets.main, 'mixins.shrines.refmap.json'
    config 'mixins.shrines.json'
    debug.verbose = true
    debug.export = true
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
                "Specification-Title"     : "shrines",
                "Specification-Vendor"    : "Silverm7ner",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "Silverm7ner",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
jar.finalizedBy('reobfJar')
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}